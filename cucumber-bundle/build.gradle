plugins {
    id 'java-library'    // needed for 'api' to expose transitive deps
    id 'maven-publish'
}

group = 'tools.dscode'
version = '7.27.2-dscode.1'

java {
    toolchain { languageVersion = JavaLanguageVersion.of(21) }
    withSourcesJar()
    withJavadocJar()
}

repositories { mavenCentral() }

ext {
    cucumberVer = '7.27.2'
    junitPlatformVer = '1.13.4'  // align all JUnit Platform artifacts
    jupiterVer = '5.11.4'
}

dependencies {
    // Your forked core in this multi-project build
    api project(':cucumber-core')

    api("io.cucumber:cucumber-java:${cucumberVer}") {
        exclude group: 'io.cucumber', module: 'cucumber-core'
    }
    api("io.cucumber:cucumber-junit-platform-engine:${cucumberVer}") {
        exclude group: 'io.cucumber', module: 'cucumber-core'
    }


    // JUnit Platform: engine/commons/launcher + Suite API for @Suite annotations
    api("org.junit.platform:junit-platform-engine:${junitPlatformVer}")
    api("org.junit.platform:junit-platform-commons:${junitPlatformVer}")
    api("org.junit.platform:junit-platform-launcher:${junitPlatformVer}")
    api("org.junit.platform:junit-platform-suite:${junitPlatformVer}")

    // JUnit Jupiter API so consumers get Assertions, etc.
    api("org.junit.jupiter:junit-jupiter-api:${jupiterVer}")


    implementation 'com.google.guava:guava:33.4.8-jre'
    implementation "com.fasterxml.jackson.datatype:jackson-datatype-guava"

    implementation 'org.junit.platform:junit-platform-launcher:1.10.3'
    implementation 'net.bytebuddy:byte-buddy:1.17.7'
    implementation 'net.bytebuddy:byte-buddy-agent:1.17.7'
    implementation 'org.slf4j:slf4j-api:2.0.16'
    implementation 'org.junit.platform:junit-platform-suite:1.10.3'
    implementation 'org.junit.jupiter:junit-jupiter:5.10.3'
    implementation 'org.junit.platform:junit-platform-launcher'
    implementation 'com.googlecode.aviator:aviator:5.4.3'
    implementation platform('com.fasterxml.jackson:jackson-bom:2.20.0')
    implementation 'com.fasterxml.jackson.core:jackson-databind'
    implementation 'com.fasterxml.jackson.core:jackson-core'
    implementation 'com.fasterxml.jackson.dataformat:jackson-dataformat-xml'
    implementation 'com.fasterxml.jackson.dataformat:jackson-dataformat-yaml'
    implementation 'com.jayway.jsonpath:json-path:2.9.0'
    implementation 'com.ibm.jsonata4java:JSONata4Java:2.6.0'
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            groupId = 'tools.dscode'
            artifactId = 'cucumber-bundle'
            version = project.version
            pom {
                name = 'cucumber-bundle (dscode turnkey)'
                description = 'Turnkey: cucumber-java + forked cucumber-core + aligned JUnit Platform (incl. Suite API)'
            }
        }
    }
    repositories { mavenLocal() }
}


javadoc {
    options.encoding = 'UTF-8'
    if (JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
    options.addStringOption('Xdoclint:none', '-quiet')
}
