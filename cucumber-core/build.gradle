plugins {
    id 'java-library'
    id 'maven-publish'
    id 'com.github.johnrengelman.shadow' version '8.1.1'
}

group = 'tools.dscode'
version = '7.27.2-dscode.1'

// Upstream builds target 8/11 for runtime; keep release = 11 for widest consumer compatibility
tasks.withType(JavaCompile).configureEach {
    options.release = (findProperty("RELEASE_TARGET") ?: "11") as int
}

dependencies {
    implementation platform("io.cucumber:cucumber-bom:${cucumberVer}")

    implementation 'io.cucumber:cucumber-gherkin'
    implementation 'io.cucumber:cucumber-gherkin-messages'
    implementation 'io.cucumber:messages'
    implementation 'io.cucumber:pretty-formatter'
    implementation 'io.cucumber:testng-xml-formatter'
    implementation 'io.cucumber:tag-expressions'
    implementation 'io.cucumber:cucumber-expressions'
    implementation 'io.cucumber:datatable'
    implementation 'io.cucumber:cucumber-plugin'
    implementation 'io.cucumber:docstring'
    implementation 'io.cucumber:html-formatter'
    implementation 'io.cucumber:junit-xml-formatter'
    implementation 'io.cucumber:ci-environment'
    implementation 'org.apiguardian:apiguardian-api'

    // Shade Jackson like upstream
    implementation platform("com.fasterxml.jackson:jackson-bom:${jacksonBom}")
    implementation 'com.fasterxml.jackson.core:jackson-databind'
    implementation 'com.fasterxml.jackson.core:jackson-core'
    implementation 'com.fasterxml.jackson.core:jackson-annotations'
    implementation 'com.fasterxml.jackson.datatype:jackson-datatype-jdk8'
}

shadowJar {
    archiveClassifier.set('') // this becomes the main artifact

    // Only embed Jackson into the shaded jar (like upstream)
    dependencies {
        include(dependency('com.fasterxml.jackson.core:jackson-databind'))
        include(dependency('com.fasterxml.jackson.core:jackson-core'))
        include(dependency('com.fasterxml.jackson.core:jackson-annotations'))
        include(dependency('com.fasterxml.jackson.datatype:jackson-datatype-jdk8'))
    }

    relocate 'com.fasterxml', 'io.cucumber.core.internal.com.fasterxml'

    // trim metadata
    exclude 'module-info.class'
    exclude 'META-INF/MANIFEST.MF'
    exclude 'META-INF/services/**'
    exclude 'META-INF/versions/**'

    manifest {
        attributes(
                'Automatic-Module-Name': 'io.cucumber.core',
                'Main-Class': 'io.cucumber.core.cli.Main'
        )
    }
}

tasks.jar.enabled = false
tasks.assemble.dependsOn tasks.shadowJar

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifact tasks.shadowJar
            groupId = 'tools.dscode'
            artifactId = 'cucumber-core'
            version = project.version
            pom {
                name = 'cucumber-core (dscode fork)'
                description = 'Drop-in replacement of cucumber-core 7.27.2 with custom modifications (includes shaded Jackson)'
            }
        }
    }
    repositories { mavenLocal() }
}
